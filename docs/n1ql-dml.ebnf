/****************
 *  n1ql dml    *
 ****************/

/*
 *  dml
 */
dml-stmt ::= insert | upsert | delete | update | merge

/*
 *  insert
 */
insert ::= 'INSERT' 'INTO' bucket-ref key-clause? (values-clause | select) returning-clause?

bucket-ref ::= (pool-name ':')? bucket-name ('AS'? alias)?

bucket-name ::= identifier

key-clause ::= 'KEY' expr

values-clause ::= 'VALUES' expr (',' expr)*

returning-clause ::= 'RETURNING' (result-expr (',' result-expr)* | 'RAW' expr)

/*
 *  upsert
 */
upsert ::= 'UPSERT' 'INTO' bucket-ref key-clause (values-clause | select) returning-clause?

/*
 *  delete
 */
delete ::= 'DELETE' 'FROM' bucket-ref keys-clause? where-clause? limit-clause? returning-clause?

/*
 *  update
 */
update ::= 'UPDATE' bucket-ref keys-clause? set-clause? unset-clause? where-clause? limit-clause? returning-clause?

set-clause ::= 'SET' path '=' expr update-for? (',' path '=' expr update-for?)*

update-for ::= 'FOR' variable 'IN' path (',' variable 'IN' path)* ('WHEN' cond)? 'END'

unset-clause ::= 'UNSET' path update-for? (',' path update-for?)*

/*
 *  merge
 */
merge ::= 'MERGE' 'INTO' bucket-ref 'USING' merge-source 'ON' key-clause merge-update? merge-delete? merge-insert? limit-clause? returning-clause?

merge-source ::= from-path ('AS'? alias)? keys-clause? | '(' ( from-term | select | values-clause ) ')' 'AS'? alias

merge-update ::= 'WHEN' 'MATCHED' 'THEN' 'UPDATE' set-clause? unset-clause? where-clause?

merge-delete ::= 'WHEN' 'MATCHED' 'THEN' 'DELETE' where-clause?

merge-insert ::= 'WHEN' 'NOT' 'MATCHED' 'THEN' 'INSERT' expr where-clause?

/*
 *  truncate
 */
truncate ::= 'TRUNCATE' 'BUCKET'? bucket-ref

/*
 *  select-for
 */
select-for ::= select 'FOR' ('UPDATE' | 'SHARE') ('OF' bucket-ref (',' bucket-ref)*)?

/*
 *  transaction-stmt
 */
transaction-stmt ::= start | commit | rollback

start ::= 'START' 'TRANSACTION'

commit ::= 'COMMIT' 'WORK'?

rollback ::= 'ROLLBACK' 'WORK'?
