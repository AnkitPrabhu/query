/****************
 *  n1ql dml    *
 ****************/

/*
 *  dml
 */
dml-stmt ::= insert | delete | update | merge

/*
 *  insert
 */
insert ::= 'INSERT' 'INTO' bucket-ref keys-clause? (((values-clause | '(' select ')') ('AS'? alias)) | select) returning-clause?

bucket-ref ::= (pool-name ':')? bucket-name ('AS'? alias)?

bucket-name ::= identifier

keys-clause ::= 'KEYS' expr

values-clause ::= 'VALUES' expr

returning-clause ::= 'RETURNING' (expr ('AS'? alias)? (',' expr ('AS'? alias)?)*)?

/*
 *  upsert
 */
upsert ::= 'UPSERT' 'INTO' bucket-ref keys-clause (((values-clause | '(' select ')') ('AS'? alias)) | select) returning-clause?

/*
 *  delete
 */
delete ::= 'DELETE' 'FROM' bucket-ref key-clause? where-clause? limit-clause? returning-clause?

/*
 *  update
 */
update ::= 'UPDATE' bucket-ref key-clause? set-clause? unset-clause? where-clause? limit-clause? returning-clause?

set-clause ::= 'SET' path '=' expr path-for? (',' path '=' expr path-for?)*

path-for ::= 'FOR' variable 'IN' path (',' variable 'IN' path)* ('WHEN' cond)? 'END'

unset-clause ::= 'UNSET' path path-for? (',' path path-for?)*

/*
 *  merge
 */
merge ::= 'MERGE' 'INTO' bucket-ref 'USING' merge-source 'ON' key-clause merge-update? merge-delete? merge-insert? limit-clause? returning-clause?

merge-source ::= from-term | '(' select ')' 'AS'? alias

merge-update ::= 'WHEN' 'MATCHED' 'THEN' 'UPDATE' set-clause? unset-clause? where-clause?

merge-delete ::= 'WHEN' 'MATCHED' 'THEN' 'DELETE' where-clause?

merge-insert ::= 'WHEN' 'NOT' 'MATCHED' 'THEN' 'INSERT' value-clause where-clause?

/*
 *  truncate
 */
truncate ::= 'TRUNCATE' 'BUCKET'? bucket-ref

/*
 *  select-for
 */
select-for ::= select 'FOR' ('UPDATE' | 'SHARE') ('OF' bucket-ref (',' bucket-ref)*)?

/*
 *  transaction-stmt
 */
transaction-stmt ::= start | commit | rollback

start ::= 'START' 'TRANSACTION'

commit ::= 'COMMIT' 'WORK'?

rollback ::= 'ROLLBACK' 'WORK'?
