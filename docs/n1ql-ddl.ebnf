/****************
 *  n1ql ddl    *
 ****************/

/*
 *  ddl
 */

ddl-stmt ::= view-index-stmt | index-stmt | bucket-stmt

view-index-stmt ::= create-view-index | drop-view-index

index-stmt ::= create-index | drop-index | alter-index

bucket-stmt ::= alter-bucket


/*
 *  view index
 */

create-view-index ::= 'CREATE' 'VIEW' 'INDEX' identifier 'ON' (pool-name ':')? bucket-name '(' subpath (',' subpath)* ')'

drop-view-index ::= 'DROP' 'VIEW' 'INDEX' (pool-name ':')? bucket-name '.' identifier


/*
 *  index
 */

create-index ::= 'CREATE' 'INDEX' identifier 'ON' (pool-name ':')? bucket-name '(' subpath (',' subpath)* ')'

drop-index ::= 'DROP' 'INDEX' (pool-name ':')? bucket-name '.' identifier

alter-index ::= 'ALTER' 'INDEX' (pool-name ':')? bucket-name '.' identifier 'RENAME' 'TO' identifier


/*
 *  bucket
 */

alter-bucket ::= 'ALTER' 'BUCKET' (pool-name ':')? bucket-name 'RENAME' subpath 'TO' identifier
