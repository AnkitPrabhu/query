/****************
 *  n1ql ddl    *
 ****************/

/*
 *  ddl
 */

ddl-stmt ::= index-stmt

index-stmt ::= create-primary-index | create-index | drop-index | alter-index


/*
 *  index
 */

create-primary-index ::= 'CREATE' 'PRIMARY' 'INDEX' 'ON' named-bucket-ref

create-index ::= 'CREATE' 'INDEX' index-name 'ON' named-bucket-ref '(' expr (',' expr)* ')' partition? where-clause? using?

index-name ::= identifier

named-bucket-ref ::= (pool-name ':')? bucket-name

partition ::= 'PARTITION' 'BY' expr

using ::= 'USING' 'VIEW'

drop-index ::= 'DROP' 'INDEX' named-bucket-ref '.' index-name

alter-index ::= 'ALTER' 'INDEX' named-bucket-ref '.' index-name 'RENAME' 'TO' index-name
